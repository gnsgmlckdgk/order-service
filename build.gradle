plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

configurations {	// 프로젝트를 빌드할 때 그래들이 설정 프로세서를 사용하도록 설정
	compileOnly {
		extendsFrom annotationProcessor
	}
}

ext {
	set('springCloudVersion', "2023.0.2")
	set('testcontainersVersion', '1.17.3')	// 사용할 테스트컨테이너 버전을 지정한다.
}

dependencies {
	// 스프링 데이터 R2DBC
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	// 유효성 검사
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	// 스프링 리액티브 웹 : 리액티브 웹 애플리케이션을 구축하기 위한 라이브러리를 제공하면 네티를 기본 임베디드 서버로 포함
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// PostgreSQL 및 R2DBC 드라이버
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'org.postgresql:r2dbc-postgresql'
	// 플라이웨이, JDBC
	//	implementation 'org.flywaydb:flyway-core'	// 포스트그리SQL 14.4 버전은 플라이웨이 최신버전에서 안됨
	implementation 'org.flywaydb:flyway-core:9.16.3'
	runtimeOnly 'org.springframework:spring-jdbc'

	// 스프링클라우드 컨피그 서버
	implementation 'org.springframework.cloud:spring-cloud-config-server'
	implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'  // Config Server 관련해서도 reactive 스택에 맞는 의존성이 필요

	implementation 'org.springframework.retry:spring-retry'					// 스프링 리트라이
	implementation 'org.springframework.boot:spring-boot-starter-actuator'	// 스프링 엑츄에이터

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// 스프링 부트 테스트
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// 테스트컨테이너
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	// 리액터 테스트
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.testcontainers:r2dbc'

	testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		// 스프링 클라우드 의존성 관리를 위한 BOM
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"	// 테스트컨테이너 의존성 관리를 위한 BOM
	}
}

bootBuildImage {	// 빌드팩을 사용해 OCI 이미지를 빌드하기 위한 스프링 부트 플러그인 작업
	imageName = "${project.name}"	// 빌드할 OCI 이미지의 이름. 이 이름을 그래들의 프로젝트 설정에서 정의한 이름과 같다. 버전 번호를 사용하지 않고 암묵적인 latest 태그를 사용한다.
	environment = ["BP_JVM_VERSION" : "17.*"]	// 이미지에 설치할 JVM 버전.

	// 컨테이너 저장소 연결을 설정하기 위한 섹션
	docker {
		publishRegistry {	// 컨테이너 저장소 인증을 설정하기 위한 섹션. 값은 그래들 속성을 통해 전달된다.
			username = project.findProperty("registryUsername")
			password = project.findProperty("registryToken")
			url = project.findProperty("registryUrl")
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}


